; Script generated by the HM NIS Edit Script Wizard.
SetCompressor bzip2

; HM NIS Edit Wizard helper defines
!define PRODUCT_NAME "Konnekt"
!define PRODUCT_PUBLISHER "Stamina"
!define PRODUCT_WEB_SITE "http://www.konnekt.info"
!define PRODUCT_DIR_REGKEY "Software\Microsoft\Windows\CurrentVersion\App Paths\konnekt.exe"
!define PRODUCT_UNINST_KEY "Software\Microsoft\Windows\CurrentVersion\Uninstall\${PRODUCT_NAME}"
!define PRODUCT_UNINST_ROOT_KEY "HKLM"
!define PRODUCT_STARTMENU_REGVAL "NSIS:StartMenuDir"

; MUI 1.67 compatible ------
!include "MUI.nsh"
; --------------------------

;!define LOGICLIB_STRCMP
;!define LOGICLIB_INT64CMP
;!define LOGICLIB_SECTIONCMP
!include "LogicLib.nsh"

!ifndef baseDir
  !define baseDir "d:\c++\konnekt\_publish"
!endif

!ifndef outName
  !define outName ""
!endif

var userType
var userComponents

Function ConvertBStoDBS2
  ;zamienia '\' na '\\' (do wyœwietlania w custom pages)
  Exch $R0 ;input string
  Push $R1 ;length string
  Push $R2 ;output string
  Push $R3 ;temp string
  Push $R4 ;counter
  Strlen $R1 $R0
  StrCmp $R1 "0" done
  StrCpy $R2 ""
  StrCpy $R4 "0"
loop:
  StrCpy $R3 $R0 1 $R4
  StrCmp $R3 "\" "0" nobs
    StrCpy $R2 "$R2$R3"
  nobs:
  StrCpy $R2 "$R2$R3"
  IntOp $R4 $R4 + 1
  IntCmp $R4 $R1 loop loop "0"
  StrCpy $R0 $R2
done:
  Pop $R4
  Pop $R3
  Pop $R2
  Pop $R1
  Exch $R0 ;output string
FunctionEnd

Function .onInit
   InitPluginsDir ;wypakowanie .ini do custom pages
   ReserveFile "Profile.ini"
   ReserveFile "Install.ini"
   !insertmacro MUI_RESERVEFILE_INSTALLOPTIONS
   !insertmacro MUI_INSTALLOPTIONS_EXTRACT "profile.ini"
   !insertmacro MUI_INSTALLOPTIONS_EXTRACT "install.ini"

   ClearErrors
   UserInfo::GetName
   IfErrors Win9x ;sprawdzenie czy przypadkiem nie jesteœmy na win9x
   Pop $0
   
   UserInfo::GetAccountType ;typ usera
   Pop $1
   
   StrCpy $userType $1

   StrCmp $userType "Admin" isAdmin isNotAdmin ;je¿eli admin to wszystko ok

   Win9x:
     !insertmacro MUI_INSTALLOPTIONS_WRITE "install.ini" "Field 5" "Flags" "DISABLED"
     
     ClearErrors
     ReadRegStr $6 HKCU "Software\Stamina\Konnekt" "installDir"
     ifErrors 0 +3
     StrCpy $INSTDIR "$PROGRAMFILES\Konnekt"
     goto finish
     StrCpy $INSTDIR $6
     goto finish

   isAdmin:
     ClearErrors
     ReadRegStr $6 HKCU "Software\Stamina\Konnekt" "installDir"
     ifErrors 0 +4
     StrCpy $INSTDIR "$PROGRAMFILES\Konnekt"
     !insertmacro MUI_INSTALLOPTIONS_WRITE "install.ini" "Field 5" "State" "1"
     goto finish
     StrCpy $INSTDIR $6
     goto finish

   isNotAdmin:
   
     !insertmacro MUI_INSTALLOPTIONS_WRITE "install.ini" "Field 5" "Flags" "DISABLED"

     MessageBox MB_ICONINFORMATION|MB_OK "Instalator wykry³, ¿e u¿ytkownik nie posiada wystarczaj¹cych uprawnieñ na tym komputerze. Jest wysoce zalecane, aby zainstalowaæ program w katalogu $INSTDIR."
     
     ClearErrors
     ReadRegStr $6 HKCU "Software\Stamina\Konnekt" "installDir"
     ifErrors 0 +3
     StrCpy $INSTDIR "$DOCUMENTS\Konnekt"
     goto finish
     StrCpy $INSTDIR $6
     goto finish
   
   finish:

FunctionEnd

Function InstallationTypePage
   ;Wybór typu instalacji
   !insertmacro MUI_HEADER_TEXT "Wybierz rodzaj instalacji" "Wybierz sposób instalacji programu."
   !ifdef noPlugs
      !ifdef Actio
         !insertmacro MUI_INSTALLOPTIONS_WRITE "install.ini" "Field 3" "Text" "Instalator zainstaluje wersjê programu tylko z wtyczk¹ rozmów telefonicznych Konnekt-Actio. Dodatki bêdziesz móg³ pobraæ poprzez system aktualizacji kUpdate."
      !else
	       !insertmacro MUI_INSTALLOPTIONS_WRITE "install.ini" "Field 3" "Text" "Instalator zainstaluje 'go³¹' wersjê programu. Dodatki bêdziesz móg³ pobraæ poprzez system aktualizacji kUpdate."
      !endif
   !else
      !ifdef noExtra
         !insertmacro MUI_INSTALLOPTIONS_WRITE "install.ini" "Field 3" "Text" "Instalator zainstaluje pe³n¹ wersjê programu."
      !else
         !insertmacro MUI_INSTALLOPTIONS_WRITE "install.ini" "Field 3" "Text" "Instalator zainstaluje pe³n¹ wersjê programu (plus dodatki)."
      !endif
   !endif
   !insertmacro MUI_INSTALLOPTIONS_DISPLAY "install.ini"

FunctionEnd

Function InstallationTypePageLeave
   !insertmacro MUI_INSTALLOPTIONS_READ $R0 "install.ini" "Field 5" "State"
   StrCmp $R0 "1" allUsers singleUser
   
   allUsers:
     SetShellVarContext all
     ;StrCpy $1 $SMPROGRAMS
     ;MessageBox MB_OK "allUsers = $1"
     goto finish
   
   singleUser:
     SetShellVarContext current
     ;StrCpy $2 $SMPROGRAMS
     ;MessageBox MB_OK "singleUser = $2"
     goto finish
   
   finish:
     
FunctionEnd

Function skipPage
   ; Pomija stronê instalatora.
   !insertmacro MUI_INSTALLOPTIONS_READ $userComponents "install.ini" "Field 2" "State"
   StrCmp $userComponents "1" noSkip
   Abort

   noSkip:
   
FunctionEnd

Function checkDir
   ; Sprawdza czy da siê zapisaæ do katalogu.
   ; Je¿eli nie, to nie pozwala przejœæ do nastêpnej strony.
   CreateDirectory "$INSTDIR"
   IfErrors abortPage 
   CreateDirectory "$INSTDIR\testowyfolder"
   IfErrors abortPage ok
   
   abortPage:
      MessageBox MB_OK "Instalator nie mo¿e zainstalowac programu do folderu: $INSTDIR. Prawdopodobnie nie masz uprawnieñ do tego folderu. Spróbuj wybraæ inn¹ lokalizacjê."
      Abort
   
   ok:
      RMDir "$INSTDIR\testowyfolder"
      RMDir "$INSTDIR"
      ClearErrors
      
FunctionEnd

Function ProfilePage
   ;Wybór loklizacji profilu.
   ClearErrors
   ReadRegStr $8 HKCU Software\Stamina\Konnekt "ProfilesDir"
   IfErrors ProfilesDirIsNotSet ProfilesDirIsSet ;Sprawdzenie rejestru

   ProfilesDirIsNotSet:
      goto ProfilesDirDone

   ProfilesDirIsSet:
      !insertmacro MUI_INSTALLOPTIONS_WRITE "profile.ini" "Field 1" "State" "0"
      !insertmacro MUI_INSTALLOPTIONS_WRITE "profile.ini" "Field 2" "State" "0"
      !insertmacro MUI_INSTALLOPTIONS_WRITE "profile.ini" "Field 3" "State" "1"
      !insertmacro MUI_INSTALLOPTIONS_WRITE "profile.ini" "Field 3" "Flags" ""
      !insertmacro MUI_INSTALLOPTIONS_WRITE "profile.ini" "Field 6" "Text" "Na tym komputerze istnieje ju¿ katalog zawieraj¹cy profile."

   ProfilesDirDone:
      StrCmp $userComponents "1" noSkipPage
      Abort

   noSkipPage:
      !insertmacro MUI_HEADER_TEXT "Wybierz lokalizacjê profilu" "Wybierz folder w jakim maj¹ znaleŸæ siê profile."

      Push "$APPDATA\stamina\konnekt\"
      Call ConvertBStoDBS2
      Pop $R0
      !insertmacro MUI_INSTALLOPTIONS_WRITE "profile.ini" "Field 4" "Text" "Lokalizacja folderu:\r\n$R0profiles"
   
      Push "$INSTDIR"
      Call ConvertBStoDBS2
      Pop $R0
      !insertmacro MUI_INSTALLOPTIONS_WRITE "profile.ini" "Field 5" "Text" "Lokalizacja folderu:\r\n$R0\\profiles"

      !insertmacro MUI_INSTALLOPTIONS_DISPLAY "profile.ini"
      
FunctionEnd

; --------------------------

BrandingText "Copyright © 2002-2005 Stamina (www.konnekt.info)"
Name "${PRODUCT_NAME} (${PRODUCT_VERSION})"

; MUI Settings

!define MUI_HEADERIMAGE
!define MUI_HEADERIMAGE_BITMAP "${baseDir}\install.bmp"
!ifndef Actio
  !define MUI_WELCOMEFINISHPAGE_BITMAP "${baseDir}\install-welcome.bmp"
!else
  !define MUI_WELCOMEFINISHPAGE_BITMAP "${baseDir}\install-welcome-actio.bmp"
!endif
!define MUI_COMPONENTSPAGE_SMALLDESC
!define MUI_ABORTWARNING
!define MUI_ICON "${baseDir}\konnekt.ico"
!define MUI_UNICON "${baseDir}\uninstall.ico"
!insertmacro MUI_PAGE_WELCOME
!ifndef olejLicencje
   !define MUI_LICENSEPAGE_CHECKBOX
   !insertmacro MUI_PAGE_LICENSE "${baseDir}\__main\licencja.rtf"
!endif
Page custom InstallationTypePage InstallationTypePageLeave
page Components skipPage
page Directory skipPage "" checkDir
Page custom ProfilePage
!insertmacro MUI_PAGE_INSTFILES
!define MUI_FINISHPAGE_RUN "$INSTDIR\konnekt.exe"

OutFile "${outDir}\konnekt_setup_${outName}${PRODUCT_VERSION}.exe"

; !ifndef Actio
InstType "Pe³na [zalecana]"
!ifndef noPlugs
InstType "Podstawowa (GG,Jabber,Tlen,LAN,SMS,DŸwiêk)"
InstType "Tylko SMS"
!endif
; !endif ;Actio

!define MUI_FINISHPAGE_SHOWREADME "$INSTDIR\doc\index.html"

!insertmacro MUI_PAGE_FINISH

; Uninstaller pages
!insertmacro MUI_UNPAGE_INSTFILES

; Language files
!insertmacro MUI_LANGUAGE "Polish"
!ifdef update
  !include include_update.nsi
!endif

; MUI end ------

InstallDirRegKey HKEY_CURRENT_USER SOFTWARE\Stamina\Konnekt "installDir"
ShowInstDetails show
ShowUnInstDetails show

; -----------------------------------
Section "!Konnekt" SKonnekt
  ;Install Files
  SectionIn 1 2 3 4 RO
  SetOutPath $INSTDIR
  SetCompress Auto
  SetOverwrite on
  AllowSkipFiles on
  File /r "${baseDir}\__main\*.*"

  CreateDirectory "$INSTDIR\profiles"
  ; Write the uninstall keys for Windows
  WriteRegStr HKCU SOFTWARE\Stamina\Konnekt "installDir" $INSTDIR
  WriteRegStr HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\Konnekt" "DisplayName" "Konnekt"
  WriteRegStr HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\Konnekt" "UninstallString" "$INSTDIR\Uninst.exe"
  WriteRegStr HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\Konnekt" "Publisher" "Stamina"
  WriteRegStr HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\Konnekt" "HelpLink" "http://www.konnekt.info/"
  WriteRegDWORD HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\Konnekt" "NoModify" 1
  WriteRegDWORD HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\Konnekt" "NoRepair" 1
  WriteUninstaller "Uninst.exe"
  
  ; Profile

  !insertmacro MUI_INSTALLOPTIONS_READ $R0 "profile.ini" "Field 3" "State" ;bez zmian
  StrCmp $R0 "1" katalogDone

  !insertmacro MUI_INSTALLOPTIONS_READ $R0 "profile.ini" "Field 1" "State"
  StrCmp $R0 "1" katalogUzytkownika katalogProgramu

  katalogUzytkownika:
  WriteRegStr HKCU Software\Stamina\Konnekt "ProfilesDir" "$APPDATA\stamina\konnekt\"
  Goto katalogDone

  katalogProgramu:
  WriteRegStr HKCU Software\Stamina\Konnekt "ProfilesDir" "profiles\"
  Goto katalogDone

  katalogDone:
  
SectionEnd
; -----------------------------------

!ifdef Actio
  Section "Actio - VoIP" SActio
    SectionIn 1 2 3 4
    File /r "${baseDir}\_actio\*.*"
  SectionEnd
!endif

!ifndef noPlugs

SubSection "Wtyczki"

  Section "!Biblioteki wtyczek" SPlugLibs
    SectionIn 1 2 3 4 RO
    File /r "${baseDir}\_LiB\*.*"
  SectionEnd


  Section "GG" SGG
    SectionIn 1 2
    File /r "${baseDir}\_gg8\*.*"
  SectionEnd

  Section "kJabber" SkJabber
    SectionIn 1 2
    File /r "${baseDir}\_kJabber\*.*"
  SectionEnd

  Section "DwuTlenek" SDwuTlenek
    SectionIn 1 2
    File /r "${baseDir}\_tlen\*.*"
  SectionEnd

  Section "SMS" SSMS
    SectionIn 1 2 3
    File /r "${baseDir}\_sms\*.*"
  SectionEnd
  
  Section "DŸwiêk" SSound
    SectionIn 1 2
    File /r "${baseDir}\_sound\*.*"
  SectionEnd

  Section "kStyle" SkStyle
    SectionIn 1
    File /r "${baseDir}\_kstyle\*.*"
  SectionEnd

  Section "kNotify" SkNotify
    SectionIn 1
    File /r "${baseDir}\_knotify\*.*"
  SectionEnd

  
  Section "kLAN" SkLAN
    SectionIn 1 2
    File /r "${baseDir}\_klan\*.*"
  SectionEnd

  Section "exPiMP" SexPiMP
    SectionIn 1 2
    File /r "${baseDir}\_expimp\*.*"
  SectionEnd


  Section "kTransfer" SkTransfer
    SectionIn 1
    File /r "${baseDir}\_ktransfer\*.*"
  SectionEnd
  
  Section "Konnferencja" SKonnferencja
    SectionIn 1
    File /r "${baseDir}\_konnferencja\*.*"
  SectionEnd
  
  Section "kIEview" SkIEview
    SectionIn 1
    File /r "${baseDir}\_kIEview\*.*"
  SectionEnd

;  Section "GGImage" SGGImage
;    SectionIn 1
;    SetOutPath "$INSTDIR\plugins"
;    File /r "${baseDir}\__add\plugins\ggimage.dll"
;    SetOutPath $INSTDIR
;  SectionEnd


!ifndef noExtra
  Section "K.Board" SKBoard
    SectionIn 1
    SetOutPath "$INSTDIR\plugins"
    File /r "${baseDir}\__add\plugins\kboard.dll"
    SetOutPath $INSTDIR
  SectionEnd


  Section "kZmieniacz" SkZmieniacz
    SectionIn 1
    SetOutPath "$INSTDIR\plugins"
    File /r "${baseDir}\__add\plugins\kzmieniacz.dll"
    SetOutPath $INSTDIR
  SectionEnd

  Section "sprz¹taczKa" SsprzataczKa
    SectionIn 1
    SetOutPath "$INSTDIR\plugins"
    File /r "${baseDir}\__add\plugins\grupy.dll"
    SetOutPath $INSTDIR
  SectionEnd
!endif


SubSectionEnd

!endif ;noPlugs

!ifndef noExtra

!endif

; -----------------------------------

Section "!Biblioteki systemowe" SLibs
   ; L¹duj¹ w katalogu konnekta.
   SectionIn 1 2 3 RO
   SetOutPath $INSTDIR
!ifndef noPlugs
; Wrzucamy wszystko z msvc
   File /r "${baseDir}\msvc\*.*"
!else
; wrzucamy tylko msvc71 i devil dla ui.dll
   File /r "${baseDir}\msvc\msvcp71.dll"
   File /r "${baseDir}\msvc\msvcr71.dll"
   SetOutPath $INSTDIR\data\dll
   File /r "${baseDir}\_Lib\data\dll\devil.dll"
   File /r "${baseDir}\_Lib\data\dll\devilu.dll"
   File /r "${baseDir}\_Lib\data\dll\devilut.dll"
   File /r "${baseDir}\_Lib\data\dll\smemory.dll"
   SetOutPath $INSTDIR
!endif
SectionEnd

; -----------------------------------

Section "Dodaj skróty" SShortcuts
  SectionIn 1 2 3
  ;Add Shortcuts
  
  ;!insertmacro MUI_INSTALLOPTIONS_READ $forAllUsers "install.ini" "Field 5" "State"
  
  CreateDirectory "$SMPROGRAMS\Konnekt"
  CreateShortCut "$SMPROGRAMS\Konnekt\Konnekt.lnk" "$INSTDIR\konnekt.exe" "" "$INSTDIR\konnekt.exe" 0
  CreateShortCut "$SMPROGRAMS\Konnekt\Konnekt - Wybierz profil.lnk" "$INSTDIR\konnekt.exe" "-profile=?" "$INSTDIR\konnekt.exe" 0
  CreateShortCut "$SMPROGRAMS\Konnekt\Konnekt - Wybierz wtyczki.lnk" "$INSTDIR\konnekt.exe" "-plugins" "$INSTDIR\konnekt.exe" 0
  CreateShortCut "$SMPROGRAMS\Konnekt\Konnekt - Uruchom w trybie offline.lnk" "$INSTDIR\konnekt.exe" "-offline" "$INSTDIR\konnekt.exe" 0
  CreateShortCut "$SMPROGRAMS\Konnekt\Konnekt - Katalog profilu.lnk" "$INSTDIR\konnekt.exe" "-profilesdir" "$INSTDIR\konnekt.exe" 0
  CreateShortCut "$SMPROGRAMS\Konnekt\Odinstaluj.lnk" "$INSTDIR\uninst.exe" "" "$INSTDIR\uninst.exe" 0
  CreateShortCut "$SMPROGRAMS\Konnekt\Strona WWW.lnk" "http://www.konnekt.info/"
  CreateShortCut "$SMPROGRAMS\Konnekt\Pomoc.lnk" "$INSTDIR\doc\index.html"
  CreateShortCut "$DESKTOP\Konnekt.lnk" "$INSTDIR\konnekt.exe" "" "$INSTDIR\konnekt.exe" 0
SectionEnd

Section "Skojarz adresy z programem" SBindings
  SectionIn 1 2 3
  
  ;jid:
  WriteRegStr HKLM "SOFTWARE\Classes\jid" "" "URL: JabberID"
  WriteRegStr HKLM "SOFTWARE\Classes\jid"  "URL Protocol" ""
  WriteRegStr HKLM "SOFTWARE\Classes\jid\shell\open\command"  "" '"$INSTDIR\konnekt.exe" -cnt=%1'
  
  ;xmpp (dla pierdolniêtych geeków):
  WriteRegStr HKLM "SOFTWARE\Classes\xmpp" "" "URL: JabberID"
  WriteRegStr HKLM "SOFTWARE\Classes\xmpp"  "URL Protocol" ""
  WriteRegStr HKLM "SOFTWARE\Classes\xmpp\shell\open\command"  "" '"$INSTDIR\konnekt.exe" -cnt=%1'

  ;gg:
  WriteRegStr HKLM "SOFTWARE\Classes\gg" "" "URL: GG"
  WriteRegStr HKLM "SOFTWARE\Classes\gg"  "URL Protocol" ""
  WriteRegStr HKLM "SOFTWARE\Classes\gg\shell\open\command"  "" '"$INSTDIR\konnekt.exe" -cnt=%1'

  ;kLAN
  WriteRegStr HKLM "SOFTWARE\Classes\lan" "" "URL: kLAN"
  WriteRegStr HKLM "SOFTWARE\Classes\lan"  "URL Protocol" ""
  WriteRegStr HKLM "SOFTWARE\Classes\lan\shell\open\command"  "" '"$INSTDIR\konnekt.exe" -cnt=%1'

  ;Tlen
  WriteRegStr HKLM "SOFTWARE\Classes\tlen" "" "URL: Tlen"
  WriteRegStr HKLM "SOFTWARE\Classes\tlen"  "URL Protocol" ""
  WriteRegStr HKLM "SOFTWARE\Classes\tlen\shell\open\command"  "" '"$INSTDIR\konnekt.exe" -cnt=%1'

SectionEnd

!ifndef noPlugs
  Section "Skojarz pliki .expimp" SBindExpimp
    SectionIn 1 2
  ;expimp
  WriteRegStr   HKCR ".expimp" "" "exPIMPFile"
  WriteRegStr   HKCR "exPIMPFile" "" "Ustawienia programu Konnekta"
  WriteRegDWORD HKCR "exPIMPFile" "BrowserFlags" "8"
  WriteRegDWORD HKCR "exPIMPFile" "EditFlags" "0"
  WriteRegStr   HKCR "exPIMPFile\DefaultIcon" "" "$INSTDIR\plugins\exPIMP.dll,0"
  WriteRegStr   HKCR "exPIMPFile\shell\open\command" "" '"$INSTDIR\konnekt.exe" -import="%1"'
  WriteRegStr   HKCR "exPIMPFile\shell\open\ddeexec\Application" "" "Konnekt"
  WriteRegStr   HKCR "exPIMPFile\shell\open\ddeexec\Topic" "" "System"
  SectionEnd
!endif

Section "Uruchamiaj razem z systemem" SAutostart
  SectionIn 1 2 3
  WriteRegStr HKEY_CURRENT_USER "Software\Microsoft\Windows\CurrentVersion\Run" "Konnekt" '"$INSTDIR\konnekt.exe" /autostart'
SectionEnd


; Section descriptions
!insertmacro MUI_FUNCTION_DESCRIPTION_BEGIN
    !insertmacro MUI_DESCRIPTION_TEXT ${SKonnekt} "Podstawowe pliki, oraz wtyczki interfejsu i pobierania nowych wersji."
    !ifndef noPlugs
    !insertmacro MUI_DESCRIPTION_TEXT ${SGG} "Komunikacja z serwerami GaduGadu."
    !insertmacro MUI_DESCRIPTION_TEXT ${SActio} "Voice over IP - bardzo tanie rozmowy g³osowe przez internet!"
    !insertmacro MUI_DESCRIPTION_TEXT ${SLibs} "Wymagane biblioteki systemowe (msvc*.dll)."
    !insertmacro MUI_DESCRIPTION_TEXT ${SCrypt} "Szyfrowanie wiadomoœci zgodne z GaduCrypt. Równie¿ dla kLAN."
    !insertmacro MUI_DESCRIPTION_TEXT ${SkLAN} "Komunikacja po sieci LAN, zgodna z WinPopup."
    !insertmacro MUI_DESCRIPTION_TEXT ${SSMS} "Przesy³anie SMSów."
    !insertmacro MUI_DESCRIPTION_TEXT ${SkTransfer} "Przesy³anie plików."
    !insertmacro MUI_DESCRIPTION_TEXT ${SkStyle} "Zmiana kolorów i ikon programu."
    !insertmacro MUI_DESCRIPTION_TEXT ${SSound} "DŸwiêki."
    !insertmacro MUI_DESCRIPTION_TEXT ${SexPiMP} "Export / Import."
    !insertmacro MUI_DESCRIPTION_TEXT ${SKonnferencja} "Prowadzenie rozmów konferencyjnych w sieci GG."
    !insertmacro MUI_DESCRIPTION_TEXT ${SkIEview} "Rozszerzenia okna rozmowy - emotikony, style graficzne (CSS), grupowanie czasu i wiele wiêcej!"
    !insertmacro MUI_DESCRIPTION_TEXT ${SkJabber} "Komunikacja z sieci¹ Jabber."
    !insertmacro MUI_DESCRIPTION_TEXT ${SDwuTlenek} "Komunikacja z sieci¹ Tlen."
    !insertmacro MUI_DESCRIPTION_TEXT ${SGGImage} "Przesy³anie obrazków w sieci GG."
    !insertmacro MUI_DESCRIPTION_TEXT ${SkNotify} "Wyskakuj¹ce powiadomienia o zdarzeniach."

    !insertmacro MUI_DESCRIPTION_TEXT ${SBindExpimp} "Pliki ustawieñ *.expimp bêd¹ automatycznie importowane po otworzeniu."
    !ifndef noExtra
    !insertmacro MUI_DESCRIPTION_TEXT ${SKBoard} "Tablica do wspólnego rysowania."
    !insertmacro MUI_DESCRIPTION_TEXT ${SkZmieniacz} "Zmiana statusów we wszystkich sieciach na raz."
    !insertmacro MUI_DESCRIPTION_TEXT ${SsprzataczKa} "Porz¹dkuje kontakty na liœcie przy pomocy 'paneli'. Dostêpne tylko w systemach win XP i nowszych."
    !endif
    !endif
    !insertmacro MUI_DESCRIPTION_TEXT ${SShortcuts} "Dodaje skróty w menu Start i na Pulpicie."
    !insertmacro MUI_DESCRIPTION_TEXT ${SAutostart} "Uruchamia program razem ze startem systemu."
    !insertmacro MUI_DESCRIPTION_TEXT ${SBindings} "W³¹cza obs³ugê linków typu gg:12345, jid:ktos@gdzies.com, tlen:konto@tlen.pl, lan:host"
!insertmacro MUI_FUNCTION_DESCRIPTION_END


Function un.onUninstSuccess
  HideWindow
  MessageBox MB_ICONINFORMATION|MB_OK "Program $(^Name) zosta³ pomyœlnie usuniêty."
FunctionEnd

Function un.onInit
  MessageBox MB_ICONQUESTION|MB_YESNO|MB_DEFBUTTON2 "Czy na pewno chcesz usun¹æ program $(^Name) i wszystkie jego komponenty?" IDYES +2
  Abort
FunctionEnd

Section Uninstall
  Delete "$SMPROGRAMS\Konnekt\*.*"
  RmDir "$SMPROGRAMS\Konnekt"

  MessageBox MB_YESNO "Czy chcesz usun¹æ wszystkie STANDARDOWE katalogi, oprócz profili?" IDNO skip_unins_files
  Delete $INSTDIR\ui.dll
  Delete $INSTDIR\konnekt.exe
  Delete $INSTDIR\changelog.txt
  Delete $INSTDIR\readme.txt
  Delete $INSTDIR\licencja.rtf
  Delete $INSTDIR\*.dll
  Delete $DESKTOP\Konnekt.lnk
  RmDir /r "$INSTDIR\doc"
  RmDir /r "$INSTDIR\plugins"
  RmDir /r "$INSTDIR\temp"
  RmDir /r "$INSTDIR\data\history"
  RmDir /r "$INSTDIR\data\dll"
  RmDir /r "$INSTDIR\data\img"
  RmDir /r "$INSTDIR\data\log"

  skip_unins_files:
  MessageBox MB_YESNO "Czy chcesz usun¹æ Profile?" IDNO skip_unins_profile
  ReadRegStr $1 HKEY_CURRENT_USER "SOFTWARE\Stamina\Konnekt" "ProfilesDir"
  RmDir /r $1
  skip_unins_profile:

  MessageBox MB_YESNO "Czy chcesz usun¹æ wszystkie pliki w katalogu programu?  Uwaga! Je¿eli przechowywa³eœ dane profili w katalogu programu - równie¿ zostan¹ usuniête!" IDNO skip_unins_dir
  RmDir /r "$INSTDIR"
  skip_unins_dir:


  MessageBox MB_YESNO "Czy na pewno chcesz usun¹æ wpisy w rejestrze?" IDNO skip_unins_reg
  DeleteRegKey HKEY_CURRENT_USER "SOFTWARE\Stamina\Konnekt"
  DeleteRegKey HKLM "SOFTWARE\Classes\jid"
  DeleteRegKey HKLM "SOFTWARE\Classes\gg"
  DeleteRegKey HKLM "SOFTWARE\Classes\lan"
  DeleteRegKey HKLM "SOFTWARE\Classes\tlen"
  DeleteRegKey HKCR ".expimp"
  DeleteRegKey HKCR "exPIMPFile"

  skip_unins_reg:

  ;Delete Uninstaller And Unistall Registry Entries
  Delete "$INSTDIR\Uninst.exe"
  DeleteRegKey HKEY_LOCAL_MACHINE "SOFTWARE\Microsoft\Windows\CurrentVersion\Uninstall\Konnekt"
  DeleteRegValue HKCU "Software\Microsoft\Windows\CurrentVersion\Run" "Konnekt"

  RMDir "$INSTDIR"
  IfFileExists "$INSTDIR\*.*" unins_notall
  Goto unins_finish
  unins_notall:
  MessageBox MB_OK "Nie wszystkie pliki zosta³y usuniête, resztki w $INSTDIR musisz usun¹æ sam."
  unins_finish:

  DeleteRegKey ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}"
  DeleteRegKey HKLM "${PRODUCT_DIR_REGKEY}"
  SetAutoClose true
SectionEnd


